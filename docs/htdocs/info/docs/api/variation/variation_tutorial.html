<html>
<head>
<title>Variation API Tutorial</title>
<meta name="order" content="3" />
</head>

<body>

<h1>Variation API Tutorial</h1>
<p>This is a tutorial about getting the Ensembl Variation data programmatically, using a Perl API. To get a description about the Ensembl Variation data, please go to this <a
href="/info/genome/variation/index.html">page</a>.
</p>

<h2 id="intro" style="font-size:large">Introduction</h2>

<p>
This tutorial is an introduction to the
<a href="./index.html">Ensembl Variation API</a>.
Knowledge of the 
<a href="/info/docs/api/core/index.html">Ensembl Core API</a> and of the concepts and conventions in the
<a href="/info/docs/api/core/core_tutorial.html">Ensembl Core API tutorial</a> is assumed.

Documentation about the Variation database schema is available 
<a href="variation_schema.html">here</a> 
, and while not necessary for this tutorial, an understanding of the database tables may help as many of the adaptor modules are table-specific.
</p>

<br />
<h2 id="code sh_perl">Code Conventions</h2>

<p>
Refer to the <a href="/info/docs/api/core/core_tutorial.html">Ensembl Core tutorial</a> for a good description of the coding conventions normally used in Ensembl. We try as much as possible to stick to these rules in Variation.
</p>

<br />
<h2 id="connect">Connecting to an Ensembl Variation database</h2>

<p>
Connecting to an Ensembl Variation database is made simple by using the Bio::EnsEMBL::Registry module:
</p>

<pre class="code sh_perl">
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
  -host =&gt; 'ensembldb.ensembl.org',
  -user =&gt; 'anonymous',
);
</pre>

<div style="background-color:#F0F0F0;border:1px solid #CCC;padding:5px;margin-bottom:20px">
  <h2 id="grch37" style="color:red">Connecting to the Ensembl Variation database for human GRCh37</h2>
  <p>
  If you are interested in data on the human <b>GRCh37</b> assembly you need to set the port number to <span style="color:red;font-weight:bold">3337</span>:
  </p>

<pre class="code sh_perl" style="margin:2px 5px 5px">
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
  -host =&gt; 'ensembldb.ensembl.org',
  -user =&gt; 'anonymous',
  -port =&gt; 3337,
);
</pre>
  
</div>

<p> The use of the registry ensures you will load the correct versions of the
Ensembl databases for the software release it can find on a database instance.
Using the registry object, you can then create any of number of database
adaptors. Each of these adaptors is responsible for generating an object of one
type. The Ensembl Variation API uses a number of object types that relate to the
data stored in the database. For example, in order to generate Variation
objects, you should first create a Variation adaptor: </p>

<pre class="code sh_perl">
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
  -host =&gt; 'ensembldb.ensembl.org',
  -user =&gt; 'anonymous',
);

my $variation_adaptor = $registry-&gt;get_adaptor(
  'human',     # species
  'variation', # database type
  'variation'  # object type
);

my $variation = $variation_adaptor-&gt;fetch_by_name('rs1333049');
</pre>

<p>
	The <b>get_adaptor</b> method will automatically create a connection to the
	relevant database; in the example above, a connection will be made to the
	Variation database for human. The three parameters passed specify the
	species, database and object type you require. Below is a non exhaustive
	list of Ensembl Variation adaptors that are most often used
</p>

<ul>
<li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1IndividualAdaptor.html">IndividualAdaptor</a> to fetch Bio::EnsEMBL::Variation::<a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Individual.html">Individual</a> objects</li>
<li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1LDFeatureContainerAdaptor.html">LDFeatureContainerAdaptor</a> to fetch LDFeatureContainerAdaptor to fetch Bio::EnsEMBL::Variation::<a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1LDFeatureContainer.html">LDFeatureContainer</a> objects</li>
<li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1PhenotypeFeatureAdaptor.html">PhenotypeFeatureAdaptor</a> to fetch Bio::EnsEMBL::Variation::<a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1PhenotypeFeature.html">PhenotypeFeature</a> objects</li>
<li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1PopulationAdaptor.html">PopulationAdaptor</a> to fetch Bio::EnsEMBL::Variation::<a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Population.html">Population</a> objects</li>
<li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1ReadCoverageAdaptor.html">ReadCoverageAdaptor</a> to fetch Bio::EnsEMBL::Variation::<a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1ReadCoverage.html">ReadCoverage</a> objects</li>
<li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1SampleAdaptor.html">SampleAdaptor</a> to fetch Bio::EnsEMBL::Variation::<a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Sample.html">Sample</a> objects</li>
<li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1TranscriptVariationAdaptor.html">TranscriptVariationAdaptor</a> to fetch Bio::EnsEMBL::Variation::<a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1TranscriptVariation.html">TranscriptVariation</a> objects</li>
<li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationAdaptor.html">VariationAdaptor</a> to fetch Bio::EnsEMBL::Variation::<a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a> objects</li>
<li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationFeatureAdaptor.html">VariationFeatureAdaptor</a> to fetch Bio::EnsEMBL::Variation::<a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a> objects</li>
</ul>

<p>
Only some of these adaptors will be used for illustration as part of this tutorial through commented perl scripts code.
</p>
<br />

<p>
The code below lists all the accessible adaptors for the Ensembl Variation API:
</p>
<pre class="code sh_perl">
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
  -host => 'ensembldb.ensembl.org',
  -user => 'anonymous',
);

my $species = 'human';
my $group = 'variation'; # database type

my $dba = $registry-&gt;get_DBAdaptor($species,$group);

my $available_adaptors = $dba-&gt;get_available_adaptors;

# Display the list of adaptors available in the Ensembl Variation API
foreach my $adaptor (sort(keys(%$available_adaptors))) {
  print $available_adaptors-&gt;{$adaptor}."\n";
}
</pre>

<br /><hr />
<h2 id="variant" style="font-size:large">Variants</h2>

<h2 id="genome">Variants in the genome</h2>

<p>
One of the most important uses for the Variation database is to be able to get all variants in a certain region in the genome.
Below is a simple commented perl script to illustrate how to get all variants in chromosome 25 in zebrafish.
</p>

<pre class="code sh_perl">
use strict;
use warnings;
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
  -host =&gt; 'ensembldb.ensembl.org',
  -user =&gt; 'anonymous'
);

my $slice_adaptor = $registry-&gt;get_adaptor('danio_rerio', 'core', 'slice'); #get the database adaptor for Slice objects
my $slice = $slice_adaptor-&gt;fetch_by_region('chromosome',25); #get chromosome 25 in zebrafish

my $vf_adaptor = $registry-&gt;get_adaptor('danio_rerio', 'variation', 'variationfeature'); #get adaptor to VariationFeature object
my $vfs = $vf_adaptor-&gt;fetch_all_by_Slice($slice); #return ALL variations defined in $slice

foreach my $vf (@{$vfs}){
  print "Variation: ", $vf-&gt;variation_name, " with alleles ", $vf-&gt;allele_string, 
        " in chromosome ", $slice-&gt;seq_region_name, " and position ", $vf-&gt;start, "-", $vf-&gt;end, "\n";
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1DBSQL_1_1SliceAdaptor.html">SliceAdaptor</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationFeatureAdaptor.html">VariationFeatureAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Slice.html">Slice</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>

<br />
<h2 id="Consequence">Consequence type of variants</h2>

<p>
Another common use of the Variation database is to retrieve the effects that
variations have on a transcript. In human, Ensembl also provides <a
href="http://sift.jcvi.org/" target="_blank">SIFT</a> and <a
href="http://genetics.bwh.harvard.edu/pph/" target="_blank">PolyPhen</a>
predictions of the effects of non-synonymous protein changes. For a complete
list of the consequence types predicted by Ensembl, <a
href="/info/genome/variation/predicted_data.html#consequences">click here</a>.
</p>

<p>Consequences are represented by a hierarchy of objects as shown below:</p>

<ul>
	<li><strong>TranscriptVariation</strong> - represents the overlap of a variation feature and a transcript
	<ul>
		<li><strong>TranscriptVariationAllele</strong> - the component represented by a particular allele of a variation
		<ul>
			<li><strong>OverlapConsequence</strong> - represents the consequence itself</li>
		</ul>
		</li>
	</ul>
	</li>
</ul>

<p>
In the example below, it is explained how to get all variants in a particular
human transcript and see what is the effect of that variant in the transcript,
including the PolyPhen and SIFT predictions. It is also shown how to retrieve
the Sequence Ontology terms for the consequences.
</p>

<pre class="code sh_perl">
use strict;
use warnings;
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
  -host =&gt; 'ensembldb.ensembl.org',
  -user =&gt; 'anonymous',
);

my $stable_id = 'ENST00000393489'; #this is the stable_id of a human transcript
my $transcript_adaptor = $registry-&gt;get_adaptor('homo_sapiens', 'core', 'transcript'); #get the adaptor to get the Transcript from the database
my $transcript = $transcript_adaptor-&gt;fetch_by_stable_id($stable_id); #get the Transcript object

my $trv_adaptor = $registry-&gt;get_adaptor('homo_sapiens', 'variation', 'transcriptvariation'); #get the adaptor to get TranscriptVariation objects
my $trvs = $trv_adaptor-&gt;fetch_all_by_Transcripts([$transcript]); #get ALL effects of Variations in the Transcript

foreach my $tv (@{$trvs}) {
  my $tvas = $tv-&gt;get_all_alternate_TranscriptVariationAlleles();

  foreach my $tva (@{$tvas}) {
    my @ensembl_consequences;
    my @so_consequences;

    my $ocs = $tva-&gt;get_all_OverlapConsequences();

    foreach my $oc (@{$ocs}) {
      push @ensembl_consequences, $oc-&gt;display_term;
      push @so_consequences, $oc-&gt;SO_term;
    }

    my $sift = $tva-&gt;sift_prediction;
    my $polyphen = $tva-&gt;polyphen_prediction;

    print "Variation ", $tv-&gt;variation_feature-&gt;variation_name,
      " allele ", $tva-&gt;variation_feature_seq,
      " has consequence ", join(",", @ensembl_consequences),
      " (SO ", join(",", @so_consequences), ").";

    if (defined($sift)) {
      print " SIFT=$sift";
    }
    if (defined($polyphen)) {
      print " PolyPhen=$polyphen";
    }

    print "\n";
  }
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1DBSQL_1_1TranscriptAdaptor.html">TranscriptAdaptor</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1TranscriptVariationAdaptor.html">TranscriptVariationAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Transcript.html">Transcript</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1TranscriptVariation.html">TranscriptVariation</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1TranscriptVariationAllele.html">TranscriptVariationAllele</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1OverlapConsequence.html">OverlapConsequence</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>

<p>
It is also possible to calculate consequence types for variants not currently in the database. In the example below, we create a VariationFeature object from scratch, given a slice and VariationFeatureAdaptor object, and use this to calculate the consequence of our new SNP. Here we use the aggregation methods in the TranscriptVariation object instead of retrieving each TranscriptVariationAllele and OverlapConsequence object.
</p>
<p>
A tool called <a href="/vep">Variant Effect Predictor (VEP)</a> is also available: this tool calculates consequence types from a list of variants provided in an input file.
</p>

<pre class="code sh_perl">
use strict;
use Bio::EnsEMBL::Registry;

# get registry
my $reg = 'Bio::EnsEMBL::Registry';
$reg-&gt;load_registry_from_db(-host => 'ensembldb.ensembl.org', -user => 'anonymous');

my $vfa = $reg-&gt;get_adaptor('human', 'variation', 'variationfeature');
my $sa = $reg-&gt;get_adaptor('human', 'core', 'slice');

# get a slice for the new feature to be attached to
my $slice = $sa-&gt;fetch_by_region('chromosome', 13);

# create a new VariationFeature object
my $new_vf = Bio::EnsEMBL::Variation::VariationFeature-&gt;new(
  -start => 114268626,
  -end => 114268626,
  -slice => $slice,           # the variation must be attached to a slice
  -allele_string => 'A/C',    # the first allele should be the reference allele
  -strand => 1,
  -map_weight => 1,
  -adaptor => $vfa,           # we must attach a variation feature adaptor
  -variation_name => 'newSNP',
);

# get the consequence types
my $cons = $new_vf-&gt;get_all_TranscriptVariations();
print "Number of TranscriptVariations: " . scalar(@{$cons}) . "\n";

foreach my $con (@{$cons}) {
  foreach my $string (@{$con-&gt;consequence_type}) {
    print
      "Variation ", $new_vf-&gt;variation_name,
      " at position ", $new_vf-&gt;seq_region_start,
      " on chromosome ", $new_vf-&gt;seq_region_name,
      " has consequence ", $string,
      " in transcript ", $con-&gt;transcript-&gt;stable_id, "\n";
  }
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1DBSQL_1_1SliceAdaptor.html">SliceAdaptor</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationFeatureAdaptor.html">VariationFeatureAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Slice.html">Slice</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Transcript.html">Transcript</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1TranscriptVariation.html">TranscriptVariation</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>


<br />
<h2 id="flanks">Variations, Flanking sequences and Genes</h2>

<p>
Below is a complete example on how to use the Variation API to retrieve different data from the database. In this particular example, 
we want to get, for a list of variant names, information about alleles, flanking sequences, locations, effects of variants
in transcripts, position in the transcript (in case it has a coding effect) and genes containing the transcripts.
</p>

<pre class="code sh_perl">
use strict;
use warnings;
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
    -host =&gt; 'ensembldb.ensembl.org',
    -user =&gt; 'anonymous'
);

my $va_adaptor = $registry-&gt;get_adaptor('human', 'variation', 'variation'); # get the different adaptors for the different objects needed
my $vf_adaptor = $registry-&gt;get_adaptor('human', 'variation', 'variationfeature');
my $gene_adaptor = $registry-&gt;get_adaptor('human', 'core', 'gene');

my @rsIds = qw(rs1367827 rs1367830);
foreach my $id (@rsIds) {
# get Variation object
  my $var = $va_adaptor-&gt;fetch_by_name($id); #get the Variation from the database using the name
  get_VariationFeatures($var);
}

sub get_VariationFeatures {
  my $var = shift;
  # get all VariationFeature objects: might be more than 1 !!!
  foreach my $vf (@{$vf_adaptor-&gt;fetch_all_by_Variation($var)}){
    print $vf-&gt;variation_name(),","; # print rsID
    print $vf-&gt;allele_string(),","; # print alleles
    print join(",",@{$vf-&gt;consequence_type()}),","; # print consequenceType
    print substr($var-&gt;five_prime_flanking_seq,-10) , "[",$vf-&gt;allele_string,"]"; #print the allele string
    print substr($var-&gt;three_prime_flanking_seq,0,10), ","; # print RefSeq
    print $vf-&gt;seq_region_name, ":", $vf-&gt;start,"-",$vf-&gt;end; # print position in Ref in format Chr:start-end
    get_TranscriptVariations($vf); # get Transcript information
  }
}

sub get_TranscriptVariations{
  my $vf = shift; 
  
  # get all TranscriptVariation objects: might be more than 1 !!!
  my $transcript_variations = $vf-&gt;get_all_TranscriptVariations; #get ALL the effects of the variation in 
                                                                    # different Transcripts
  if (defined $transcript_variations){
    foreach my $tv (@{$transcript_variations}){
      print ",", $tv-&gt;pep_allele_string if (defined $tv-&gt;pep_allele_string);
                                              # the AA change, but only if it is in a coding region
      my $gene = $gene_adaptor-&gt;fetch_by_transcript_id($tv-&gt;transcript-&gt;dbID);
      print ",",$gene-&gt;stable_id if (defined $gene-&gt;external_name); # and the external gene name
    }
  }
  print "\n";
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1DBSQL_1_1GeneAdaptor.html">GeneAdaptor</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationAdaptor.html">VariationAdaptor</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationFeatureAdaptor.html">VariationFeatureAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Gene.html">Gene</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Transcript.html">Transcript</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1TranscriptVariation.html">TranscriptVariation</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>


<br />
<h2 id="failed_variants">Failed variants</h2>

<p>
	In the Variation pipeline, quality checks are performed in order to remove variants that contain errors or inconsistencies.
	These variants are flagged as being failed but all related data is kept in the database and can be retrieved via the API or the web interface.
</p>
<p>
	The default API behaviour when fetching multiple objects is not to return data for variants that have been flagged as failed but
	this can be modified in the Bio::EnsEMBL::Variation::DBSQL::DBAdaptor module by setting the <b>include_failed_variations</b> flag. This
	will affect all adaptors that have been created	(and will be created) using the connection until the behaviour is explicitly changed
	again (or the connection to the database is closed).
</p>
<p>
	Below it is a simple commented perl script to illustrate how to get data related to variants that have been flagged as failed.
</p>

<pre class="code sh_perl">
use strict;
use warnings;
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
  -host =&gt; 'ensembldb.ensembl.org',
  -user =&gt; 'anonymous'
);

# Get the database adaptor for Slice objects for human
my $slice_adaptor = $registry-&gt;get_adaptor('human', 'core', 'slice');
# Get a slice from chromosome 6
my $slice = $slice_adaptor-&gt;fetch_by_region('chromosome','6',133017695,133161157); 

# Get adaptor to VariationFeature object
my $vf_adaptor = $registry-&gt;get_adaptor('human', 'variation', 'variationfeature');

# First, get all variations on the slice. The default behaviour is to exclude variants that have been flagged as failed
my $vfs = $vf_adaptor-&gt;fetch_all_by_Slice($slice); 

# Count the number of variants returned
print "There are " . scalar(@{$vfs}) . " variants in the " . $slice-&gt;display_id() . " region\n";

# Now, tell the DBAdaptor that we want the failed variations as well. We get the DBAdaptor via the db() subroutine in our adaptor
$vf_adaptor-&gt;db-&gt;include_failed_variations(1);

# Again, fetch all variants on the slice but this time include the failed variants
$vfs = $vf_adaptor-&gt;fetch_all_by_Slice($slice); 

# Count the number of variants returned, this number is likely to be higher than when failed variants were omitted
print "There are " . scalar(@{$vfs}) . " variants in the " . $slice-&gt;display_id() . " region\n";
</pre>

<p>
	There are a few subroutines to query the failed status of a variant. In the code snippet below we loop over the
	variants we got and find out how many are flagged as failed and why.
</p>

<pre class="code sh_perl">
my $failed = 0;
my %descriptions;
foreach my $vf (@{$vfs}) {
  # Check whether the variation for this variation feature is failed
  if ($vf-&gt;variation-&gt;is_failed()) {
    $failed++;
    # Get the reason why the variation failed and count the number of variations failed for the same reason.
    map {$descriptions{$_}++} @{$vf-&gt;variation-&gt;get_all_failed_descriptions()};
  }
}

# Print the results
print "There are $failed variants on slice " . $slice-&gt;display_id() . " that have been flagged as failed:\n";
map { print "\t" . $descriptions{$_} . " variants failed because " . $_ . "\n"; } keys(%descriptions);
</pre>

<p>
	<span style="color:red;font-weight:bold">NOTE: </span>some fetch methods are unaffected by the <b>include_failed_variations</b> flag in DBAdaptor. These are methods that
	return data related to specific variants, rather than a general batch fetch. For example, the fetch_by_dbID and
	fetch_all_by_VariationFeatures methods in TranscriptVariationAdaptor will not be affected by the flag whereas the
	fetch_all_by_Transcripts method will be.
</p>
<p>
	Even though a variant has not been failed in the Variation pipeline, some of the alleles associated with it may have been. 
	Variants having failed alleles are not themselves flagged as failed but we can query them to see if any associated alleles 
	have been flagged as failed.
</p>

<pre class="code sh_perl">
foreach my $vf (@{$vfs}) {
  # Check whether the variation for this variation feature has failed alleles
  if ($vf-&gt;variation-&gt;has_failed_alleles()) {
    # Get all the alleles associated with the variation and check which ones have failed and why
    my %failed_alleles;

    foreach my $allele (@{$vf-&gt;variation-&gt;get_all_Alleles()}) {
      # Check if the allele is flagged as failed
      if ($allele-&gt;is_failed()) {
        # Get the reason why
        $failed_alleles{$allele-&gt;subsnp() . ":" . $allele-&gt;allele()} = $allele-&gt;failed_description();
      }
    } 

    # Print the result
    print "Variation " . $vf-&gt;variation_name() . " has the following failed alleles:\n";
    map { print "\t" . $_ . " has been flagged as failed because " . $failed_alleles{$_} . "\n"; } keys(%failed_alleles);
  }
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1DBSQL_1_1SliceAdaptor.html">SliceAdaptor</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationFeatureAdaptor.html">VariationFeatureAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Slice.html">Slice</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Allele.html">Allele</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>

<br />
<h2 id="phenotype">Phenotype annotations</h2>

<p>
The Ensembl Variation API provides some methods to retrieve phenotype data
associated with various Ensembl object types (variations, structural variations,
genes, and QTLs). These data, stored into the Ensembl Variation database, are
imported from different sources (e.g. NHGRI-EBI GWAS catalog, OMIM, UniProt, EGA, COSMIC, HGMD, ...).</p>
<pre class="code sh_perl">
use strict;
use warnings;
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
  -host => 'ensembldb.ensembl.org',
  -user => 'anonymous'
);

# Fetch a variation object
my $var_adaptor = $registry-&gt;get_adaptor('human', 'variation', 'variation');
my $var = $var_adaptor-&gt;fetch_by_name('rs1421085');

# Fetch all the phenotype features associated with the variation
my $pf_adaptor = $registry-&gt;get_adaptor('homo_sapiens', 'variation', 'phenotypefeature');
foreach my $pf (@{$pf_adaptor-&gt;fetch_all_by_Variation($var)}) {
  print "Variation ", $pf-&gt;variation_names, " is associated with the phenotype '", $pf-&gt;phenotype-&gt;description,
    "' in source ", $pf-&gt;source_name;

  print " with a p-value of ",$pf-&gt;p_value if (defined($pf-&gt;p_value));

  print ".\n";

  print "The risk allele is ", $pf-&gt;risk_allele, ".\n" if (defined($pf-&gt;risk_allele));
}
</pre>

<p>It is also possible to retrieve all features associated with a phenotype
description and an optional source name.</p>

<pre class="code sh_perl">
my $source_name = 'NHGRI-EBI GWAS catalog';
my $phenotype   = 'Cardiac hypertrophy';

my $pf_adaptor = $registry-&gt;get_adaptor('homo_sapiens', 'variation', 'phenotypefeature');
 
foreach my $pf (@{$pf_adaptor-&gt;fetch_all_by_phenotype_description_source_name($phenotype, $source_name)}) {
  my $external_reference = $pf-&gt;external_reference;
  $external_reference =~ s/\// ID: /;

  print $pf-&gt;type, " ", $pf-&gt;object_id, " is associated with the phenotype '", $phenotype,
    "' in source ", $source_name," (", $external_reference,")\n";
}		
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationAdaptor.html">VariationAdaptor</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1PhenotypeFeatureAdaptor.html">PhenotypeFeatureAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1PhenotypeFeature.html">PhenotypeFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Phenotype.html">Phenotype</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>


<br />
<h2 id="variant_sets">Variant sets</h2>
<p>
	Variant sets <b>group variants that share some properties</b> i.e. from the same project/study, individual, phenotype, chip, to make it easier to retrieve these for analysis.<br />
	<i>> For example, all the variants identified in the 1000 Genomes - phase 3 study have been grouped into one variation set ('1000 Genomes 3 - All').</i>
</p>
<p>
	Sets can be be part of supersets or divided into subsets depending on the  hierarchical relationships between them.<br />
	<i>> For example, the set representing variations identified in the 1000 Genomes - phase 3 study is named 
	'1000 genomes 3 - All' and has 5 subsets based on population: '1000 Genomes 3 - EUR', '1000 Genomes 3 - EAS', '1000 Genomes 3 - SAS', '1000 Genomes 3 - AMR', '1000 Genomes 3 - AFR'.</i>
</p>
<p>A list of all the variant sets with their description is available on the <a href="/info/genome/variation/data_description.html#variation_sets">Variation Data Description</a> page.</p>

<p>
<span style="color:red;font-weight:bold">NOTE: </span>The variant sets and the populations are differents! e.g. The variant set associated with the European 1000 Genomes data is "1000 Genomes 3 - EUR", whereas the associated population is "1000GENOMES:phase_3:EUR".
</p>

<p>
	In the Ensembl Variation API, variant sets are handled by the module <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationSet.html">VariationSet</a>, and the accompanying 
	adaptor module, <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationSetAdaptor.html">VariationSetAdaptor</a>, that interfaces with the underlying database. A VariationSet has
	a name attribute, a brief description and also a short name attribute. In the example below, we will connect to a Human Variation database, get a VariationSetAdaptor, fetch all available variant sets and 
	print out their names and hierarchical relationships. 
</p>
<p>
	First, we get a connection and an adaptor:
</p>	
<pre class="code sh_perl">
use strict;
use warnings;
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

# Load the registry from db
$registry-&gt;load_registry_from_db(
  -host => 'ensembldb.ensembl.org',
  -user => 'anonymous'
);

# Get a VariationSetAdaptor on the Human Variation database
my $vs_adaptor = $registry-&gt;get_adaptor('human','variation','variationset');
</pre>
<p>
	Next, we get the VariationSet objects from the adaptor. In order to display the hierarchical relationships between VariationSets, we fetch only the top level sets and for each set, recursively
	get the subsets. We print the name and short name attributes for each set and indicate the hierarchy by increasing indentation.
</p>
<pre class="code sh_perl">
# Get all top-level variation sets
my $top_vss = $vs_adaptor-&gt;fetch_all_top_VariationSets();

# Loop over the top level variant sets and recursively print the subsets
foreach my $top_vs (@{$top_vss}) {
  print_set($top_vs);
}

# We define a function that will help us recurse over the set hierarchy and print the data   
sub print_set {
  my $set = shift;
  my $indent = shift || "";

  # Print the set attributes
  printf("\%s\%s (\%s)\n",$indent,$set-&gt;name(),$set-&gt;short_name());

  # Get the subsets that have the current set as immediate parent
  my $subsets = $set-&gt;get_all_sub_VariationSets(1);

  # Call the print subroutine for each of the subsets with an increased indentation
  foreach my $subset (@{$subsets}) {
    print_set($subset,$indent . "  ");
  }
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationSetAdaptor.html">VariationSetAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationSet.html">VariationSet</a></li> 
    </ul>
  </div>
  <div style="clear:both"></div>
</div>


<p>
	Running the code in the example above on the Human Variation database produces a table similar to the one on
	the <a href="/info/genome/variation/data_description.html#variation_sets">Variation Data Description</a> page.
</p>
<p>
	In order to get objects for the variants assigned to a variation set, we use the get_Variation_Iterator method. Note that rather than returning a reference to a list of all variation objects, this 
	returns an <a href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Utils_1_1Iterator.html">Iterator</a> object. The Iterator allows you to iterate over large numbers of objects without trying to fit them all into
	memory at once which could otherwise easily cause your script to crash. The main methods we will use on the iterator are the has_next() and next() methods. In the example below, we fetch a variant set that contains  
	variants that have been linked to phenotypes and print the first 10 examples. Note that getting variants from a variant set that has subsets below it automatically returns the variants from the subsets.  
</p>

<pre class="code sh_perl">
# Get the variant set for the phenotype-associated variants.
my $vs = $vs_adaptor-&gt;fetch_by_short_name('ph_variants');

printf("\%s (\%s):\n\t\%s\n",$vs-&gt;name(),$vs-&gt;short_name(),$vs-&gt;description());

my $limit = 10;
my $fetched = 0;
my $it = $vs-&gt;get_Variation_Iterator();

# Get the first 10 examples and print some data from them
while ($fetched &lt; $limit &amp;&amp; $it-&gt;has_next()) {
  
  # Print the name of the variant
  my $var = $it-&gt;next();
  printf("\t\%s has been found to be associated with:\n",$var-&gt;name());
  
  # Get the PhenotypeFeature objects for the variant
  my $pfs = $var-&gt;get_all_PhenotypeFeatures();
  
  # Loop over the annotations and print the phenotypes
  foreach my $pf (@{$pfs}) {
    my @output = ();
    push @output, 'Desc: ' . $pf-&gt;phenotype-&gt;description;
    push @output, 'p-value: ' . $pf-&gt;p_value if (defined $pf-&gt;p_value);
    push @output, 'study_type: ' . $pf-&gt;study-&gt;type if (defined $pf-&gt;study && defined $pf-&gt;study-&gt;type);
    push @output, 'study_name: ' . $pf-&gt;study-&gt;name if (defined $pf-&gt;study && defined $pf-&gt;study-&gt;name);
    print join(' ', @output), "\n";
  }
  $fetched++;
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationSetAdaptor.html">VariationSetAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Phenotype.html">Phenotype</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1PhenotypeFeature.html">PhenotypeFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Study.html">Study</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationSet.html">VariationSet</a></li>
      
    </ul>
  </div>
  <div style="clear:both"></div>
</div>


<p>
	You can also use Slices to retrieve VariationFeatures belonging to a desired variant set in a particular genomic region. 
	In the example below, we fetch all VariationFeatures discovered by the 1000 Genomes Phase 3 Project on the RIC8A gene.
</p>
<pre class="code sh_perl">
# Get the variation set object
my $vs = $vs_adaptor-&gt;fetch_by_short_name('1kg_3');

# Get a gene adaptor and the gene object for the RIC8A gene
my $gene_adaptor = $registry-&gt;get_adaptor('human','core','gene');
my $gene = $gene_adaptor-&gt;fetch_by_display_label('RIC8A');

# Get the variation features on the slice belonging to the variation set
my $vfs = $vs-&gt;get_all_VariationFeatures_by_Slice($gene-&gt;feature_Slice());

# Loop over the variation features and print the variation using HGVS notation relative to the canonical transcript
foreach my $vf (@{$vfs}) {
  my $tvs = $vf-&gt;get_all_TranscriptVariations([$gene-&gt;canonical_transcript()]);
  my $tv = $tvs-&gt;[0];
  my @hgvs_notations = ();
  foreach my $hgvs_type (qw/hgvs_genomic hgvs_transcript hgvs_protein/) {
    for my $hgvs_notation (values %{$tv-&gt;$hgvs_type}) {
      push @hgvs_notations, ((defined $hgvs_notation) ? $hgvs_notation : "no $hgvs_type");
    }
  }
  print $vf-&gt;variation_name, ': ', join(', ', @hgvs_notations), "\n";
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1DBSQL_1_1GeneAdaptor.html">GeneAdaptor</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationSetAdaptor.html">VariationSetAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Gene.html">Gene</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Slice.html">Slice</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1TranscriptVariation.html">TranscriptVariation</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationSet.html">VariationSet</a></li>
      
    </ul>
  </div>
  <div style="clear:both"></div>
</div>


<p>
The variant sets can also be retrieved from the structural variants.
</p>
<pre class="code sh_perl">
# Get the StructuralVariation object
my $sv_adaptor  = $registry-&gt;get_adaptor( 'human', 'variation', 'structuralvariation' );
my $sv = $sv_adaptor-&gt;fetch_by_name('esv3626563');

# Get the structural variation features
my $svfs = $sv-&gt;get_all_StructuralVariationFeatures();

# Loop over the structural variation features and print the corresponding variant set(s)
foreach my $svf (@{$svfs}) {
  my $vss = $svf-&gt;get_all_VariationSets();
  foreach my $vs (@{$vss}) {
    print "The structural variation belongs to the set ", $vs-&gt;name, "\n";
  }
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1StructuralVariationAdaptor.html">StructuralVariationAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1StructuralVariation.html">StructuralVariation</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1StructuralVariationFeature.html">StructuralVariationFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationSet.html">VariationSet</a></li>
      
    </ul>
  </div>
  <div style="clear:both"></div>
</div>


<br /><hr />
<h2 id="ag" style="font-size:large">Alleles and Genotypes</h2>

<div style="width:800px" class="info">
  <h3>Requirement for 1000 Genomes data and other large genotyping projects</h3>
  <div class="message-pad">
    <p>1000 Genomes Project Phase 3 data is accessed via VCF files stored on Ensembl's FTP server. The API makes this access seamless, though it requires you install some extra software dependencies detailed on the <a href="/info/docs/api/api_installation.html#variation">API installation page</a>. See also this <a href="http://www.ensembl.info/blog/2015/06/18/1000-genomes-phase-3-frequencies-genotypes-and-ld-data/" rel="external">blog post</a>.
    </p>
  </div>
</div>

<h2 id="alleles">Alleles and frequencies</h2>

<p>
Most variants in the Ensembl Variation database have associated allele frequencies. Each allele object associated with a variant represents an observation of a variant allele in a given population, and may have an associated frequency. Populations are also represented as objects.
</p>

<pre class="code sh_perl">
use strict;
use warnings;
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
  -host =&gt; 'ensembldb.ensembl.org',
  -user =&gt; 'anonymous'
);

my $variation_adaptor = $registry-&gt;get_adaptor('mus_musculus', 'variation', 'variation');

my $variation = $variation_adaptor-&gt;fetch_by_name('rs4224828');
my $alleles = $variation-&gt;get_all_Alleles();

foreach my $allele (@{$alleles}) {
  next unless (defined $allele-&gt;population);
  my $allele_string   = $allele-&gt;allele;
  my $frequency       = $allele-&gt;frequency || 'NA';
  my $population_name = $allele-&gt;population-&gt;name;
  printf("Allele %s has frequency: %s in population %s.\n", $allele_string, $frequency, $population_name);
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationAdaptor.html">VariationAdaptor</a></li>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Allele.html">Allele</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Population.html">Population</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>

<div class="info">
  <h3>Retrieve external data (e.g. 1000 Genomes phase 3)</h3>
  <div class="message-pad">
    <p>To retrieve external data e.g. from the 1000 Genomes phase 3 in human, remember to set the flag <b>"use_vcf"</b> to <span style="color:#F00;font-weight:bold">1</span> before making a call to retrieve alleles, genotypes or LD data. See an example in the following code:</p>
    <pre class="code sh_perl">my $variation_adaptor = $registry-&gt;get_adaptor("human", "variation", "variation");

# Set a flag on the variation adaptor to use VCF files for genotype extraction as well as the MySQL database
$variation_adaptor-&gt;db-&gt;use_vcf(1);</pre>
    <p>This instructs the API to integrate data fetched from VCF files alongside data from the Ensembl MySQL databases. No further configuration is required; the API is pre-configured to access indexed VCF files on the Ensembl FTP server.
    </p>
  </div>
</div>


<br />
<h2 id="sample_genotypes">Sample genotypes</h2>

<p>
Many variants in the Ensembl Variation database, especially those genotyped in the HapMap or 1000 Genomes projects, have associated genotypes. Genotypes are stored per sample as <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1SampleGenotype.html">SampleGenotype</a> objects, and are associated with a <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a> and <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Sample.html">Sample</a> object. They can be retrieved from a <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1SampleGenotypeAdaptor.html">SampleGenotypeAdaptor</a>, or directly from a <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a> object.
</p>

<pre class="code sh_perl">
use strict;
use warnings;
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
    -host =&gt; 'ensembldb.ensembl.org',
    -user =&gt; 'anonymous'
);

my $variation_adaptor = $registry-&gt;get_adaptor('homo_sapiens', 'variation', 'variation');

# OPTIONAL: uncomment the following line to retrieve 1000 genomes phase 3 data also
# $variation_adaptor-&gt;db-&gt;use_vcf(1);

my $variation = $variation_adaptor-&gt;fetch_by_name('rs1333049');

my $genotypes = $variation-&gt;get_all_SampleGenotypes();

foreach my $genotype (@{$genotypes}) {
  print "Sample ", $genotype-&gt;sample-&gt;name, " has genotype ", $genotype-&gt;genotype_string, "\n";
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationAdaptor.html">VariationAdaptor</a></li>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Sample.html">Sample</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1SampleGenotype.html">SampleGenotype</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>


<br />

<h2 id="sample_genotype_features">Sample genotype features</h2>
<p>
The <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1SampleGenotypeFeatureAdaptor.html">SampleGenotypeFeatureAdaptor</a> provides methods for retrieving <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1SampleGenotypeFeature.html">SampleGenotypeFeature</a> objects in a genomic region. A <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1SampleGenotypeFeature.html">SampleGenotypeFeature</a> stores information about the <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1SampleGenotype.html">SampleGenotype</a> and the genomic location of the <a href="/info/docs/Doxygen/variation-api/">SampleGenotype</a> which can be accessed through the <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a> object that is stored in a <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1SampleGenotypeFeature.html">SampleGenotypeFeature</a> object. The <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1SampleGenotypeFeatureAdaptor.html">SampleGenotypeFeatureAdaptor</a> provides methods for retrieving only unique genotypes for a given <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Sample.html">Sample</a> and <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Population.html">Population</a> and a method that returns all <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1SampleGenotypeFeature.html">SampleGenotypeFeatures</a> in a region and also stores genotypes for samples that differ from the given sample's genotype and are also different from the reference. 
</p>

<pre class="code sh_perl">
use strict;
use warnings;
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
  -host =&gt; 'ensembldb.ensembl.org',
  -user =&gt; 'anonymous'
);

my $sgfa = $registry-&gt;get_adaptor('mouse', 'variation', 'samplegenotypefeature');
$sgfa-&gt;db-&gt;use_vcf(1);

my $slice_adaptor = $registry-&gt;get_adaptor('mouse', 'core', 'slice');
my $chr = 18;
my $start = 68_260_187;
my $end = 68_300_333;
my $slice = $slice_adaptor-&gt;fetch_by_region('chromosome', $chr, $start, $end);

my $pa = $registry-&gt;get_adaptor('mouse', 'variation', 'population');
my $sa = $registry-&gt;get_adaptor('mouse', 'variation', 'sample');

my $population = $pa-&gt;fetch_by_name('Mouse Genomes Project');
my $samples = $sa-&gt;fetch_all_by_name('MGP:CAST/EiJ');
my $sample = $samples-&gt;[0];

my $unique_sgfs = $sgfa-&gt;fetch_all_unique_by_Slice($slice, $sample, $population);
foreach my $sgf (@$unique_sgfs) {
  my $genotype_string = $sgf-&gt;genotype_string;
  my $vf = $sgf-&gt;variation_feature;
  my $variation_name = $vf-&gt;variation_name;
  my $seq_region_name = $vf-&gt;seq_region_name;
  my $seq_region_start = $vf-&gt;seq_region_start;
  my $seq_region_end = $vf-&gt;seq_region_end;
  my $allele_string = $vf-&gt;allele_string;
  print "$variation_name $genotype_string $allele_string $seq_region_name $seq_region_start $seq_region_end\n";
}

my $differences_sgfs = $sgfa-&gt;fetch_all_differences_by_Slice($slice, $sample, $population);
foreach my $sgf (@$differences_sgfs) {
  my $genotype_string = $sgf-&gt;genotype_string;
  my $vf = $sgf-&gt;variation_feature;
  my $variation_name = $vf-&gt;variation_name;
  my $seq_region_name = $vf-&gt;seq_region_name;
  my $seq_region_start = $vf-&gt;seq_region_start;
  my $seq_region_end = $vf-&gt;seq_region_end;
  my $allele_string = $vf-&gt;allele_string;
  print "$variation_name $genotype_string $allele_string $seq_region_name $seq_region_start $seq_region_end\n";

  my $differences = $sgf-&gt;differences;
  foreach my $sample_name (keys %$differences) {
    my $genotype = $differences-&gt;{$sample_name};
    print "   $sample_name $genotype\n";
  }
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1SampleGenotypeFeatureAdaptor.html">SampleGenotypeFeatureAdaptor</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1SampleAdaptor.html">SampleAdaptor</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1PopulationAdaptor.html">PopulationAdaptor</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1DBSQL_1_1SliceAdaptor.html">SliceAdaptor</a> <i>(Core API)</i></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1SampleGenotypeFeature.html">SampleGenotypeFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Sample.html">Sample</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Population.html">Population</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>

<br />

<h2 id="population_genotypes">Population genotypes</h2>

<p>
Many variants in the Ensembl Variation database, especially those genotyped in the HapMap or 1000 Genomes projects, have associated genotypes at the population level. Population genotypes are stored as <a href="/info/docs/Doxygen/variation-api/classBio_3_1EnsEMBL_1_1Variation_1_1PopulationGenotype.html">PopulationGenotype</a> objects, and are associated with a <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a> and a <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Population.html">Population</a> object. They can be retrieved from a <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1PopulationGenotypeAdaptor.html">PopulationGenotypeAdaptor</a>, or directly from a <a href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a> object.
</p>

<pre class="code sh_perl">
use strict;
use warnings;
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
    -host =&gt; 'ensembldb.ensembl.org',
    -user =&gt; 'anonymous'
);

my $variation_adaptor = $registry-&gt;get_adaptor('homo_sapiens', 'variation', 'variation');

# OPTIONAL: uncomment the following line to retrieve 1000 genomes phase 3 data also
# $variation_adaptor-&gt;db-&gt;use_vcf(1);

my $variation = $variation_adaptor-&gt;fetch_by_name('rs1333049');

my $genotypes = $variation-&gt;get_all_PopulationGenotypes();

foreach my $genotype (@{$genotypes}) {
  print "Population ", $genotype-&gt;population-&gt;name, " has genotype ", $genotype-&gt;genotype_string, " with the frequency ", $genotype-&gt;frequency, "\n";
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationAdaptor.html">VariationAdaptor</a></li>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Population.html">Population</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1PopulationGenotype.html">PopulationGenotype</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>

<br />
<h2 id="ld">LD calculation</h2>

<div style="width:800px" class="info">
  <h3>Requirement</h3>
  <div class="message-pad">
	  <p>In order to be able to use the LD calculation, you need to compile the C source code and install a Perl module, called <b>IPC::Run</b>.<br />There is more information
       on how to do this in <a href="https://raw.github.com/Ensembl/ensembl-variation/release/[[SPECIESDEFS::ENSEMBL_VERSION]]/C_code/README.txt" rel="external">here</a>.
		</p>
	</div>
</div>

<p>
In the example below, it calculates the LD in a region in human chromosome 6 for a 1000 Genomes project population, but only prints when there is a high LD
</p>

<pre class="code sh_perl">
use strict;
use warnings;
use Bio::EnsEMBL::Registry;

my $registry = 'Bio::EnsEMBL::Registry';

$registry-&gt;load_registry_from_db(
  -host =&gt; 'ensembldb.ensembl.org',
  -user =&gt; 'anonymous'
);

my $chr = 6;  #defining the region in chromosome 6
my $start = 25_834_000;
my $end = 25_854_000;
my $population_name = '1000GENOMES:phase_3:CEU'; #we only want LD in this population

my $slice_adaptor = $registry-&gt;get_adaptor('human', 'core', 'slice'); #get adaptor for Slice object
my $slice = $slice_adaptor-&gt;fetch_by_region('chromosome',$chr,$start,$end); #get slice of the region

my $population_adaptor = $registry-&gt;get_adaptor('human', 'variation', 'population'); #get adaptor for Population object
my $population = $population_adaptor-&gt;fetch_by_name($population_name); #get population object from database

my $ldFeatureContainerAdaptor = $registry-&gt;get_adaptor('human', 'variation', 'ldfeaturecontainer'); #get adaptor for LDFeatureContainer object
# Retrieve 1000 genomes phase 3 data from VCF file
$ldFeatureContainerAdaptor-&gt;db-&gt;use_vcf(1);
my $ldFeatureContainer = $ldFeatureContainerAdaptor-&gt;fetch_by_Slice($slice,$population); #retrieve all LD values in the region

foreach my $r_square (@{$ldFeatureContainer-&gt;get_all_r_square_values}){
  if ($r_square-&gt;{r2} &gt; 0.8){ #only print high LD, where high is defined as r2 &gt; 0.8
    print "High LD between variations ", $r_square-&gt;{variation1}-&gt;variation_name, "-", $r_square-&gt;{variation2}-&gt;variation_name, "\n";
  }
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1DBSQL_1_1SliceAdaptor.html">SliceAdaptor</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1LDFeatureContainerAdaptor.html">LDFeatureContainerAdaptor</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1PopulationAdaptor.html">PopulationAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Slice.html">Slice</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1LDFeatureContainer.html">LDFeatureContainer</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Population.html">Population</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>

<p>
Please note the following warning <code> MSG: variation features have no pairwise ld information </code> is returned when there is no data in the LDFeatureContainer structure for the specified pair of VariationFeature objects. Here are some reasons why this may happen:
</p>
<ul>
<li>the LDFeatureContainer does not span the region contained by one or both VariationFeature objects specified</li>
<li>there are no genotypes for one or both variants, meaning no LD can be calculated</li>
<li>one or both variants is non-variant in the specified population (i.e. has a minor allele frequency of 0)</li>
<li>the estimated r<sup>2</sup> value is lower than the cut-off threshold in the calculation software (the threshold is 0.05)</li>
</ul>
<p> Looking at other associated data will help to narrow down the reason. If there are variants in the region spanned by the LDFeatureContainer 
with genotypes with a minor allele frequency greater than zero, then assume the r<sup>2</sup> is &lt; 0.05, which essentially means no correlation. </p>


<br />
<h2 id="strains">Specific strain information</h2>

<p>
With the apparition of the new technologies, one of the new functionalities that the Ensembl Variation API has is the possibility to
work with your specific strain as if it was the reference one, and compare it against others. In the example, we create a StrainSlice
object for a region in the mouse strain A/J and compare it against the reference sequence.
</p>

<pre class="code sh_perl">
use strict;
use warnings;

use Bio::EnsEMBL::Registry;

my $reg = 'Bio::EnsEMBL::Registry';
my $host= 'ensembldb.ensembl.org';
my $user= 'anonymous';

$reg-&gt;load_registry_from_db(
  -host =&gt; $host,
  -user =&gt; $user
);

# get exon adaptor from core
my $sa = $reg-&gt;get_adaptor("mouse", "core", "slice");

my $slice = $sa-&gt;fetch_by_region('chromosome', 19, 20380186, 20384187);

# get strainSlice from the slice
my $a_j = $slice-&gt;get_by_strain("A/J");

my @differences = @{$a_j-&gt;get_all_AlleleFeatures_Slice()};

foreach my $diff (@differences){
  print "Locus ", $diff-&gt;seq_region_start, "-", $diff-&gt;seq_region_end, ", A/J's alleles: ", $diff-&gt;allele_string, "\n";
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1DBSQL_1_1SliceAdaptor.html">SliceAdaptor</a> <i>(Core API)</i></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Slice.html">Slice</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1AlleleFeature.html">AlleleFeature</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>


<br /><hr />
<h2 id="sv" style="font-size:large">Structural variants</h2>

<h2 id="structural">Structural variations</h2>
<p>
The Ensembl Variation database also stores information about structural variants. These data are imported from the DGVa (Database of Genomic Variants archive). Structural variants are remapped to the current genome assembly using the $slice-&gt;project() method from the Ensembl Core API, allowing for one gap in the resultant mapping.</p>

<pre class="code sh_perl">
use strict;
use warnings;

use Bio::EnsEMBL::Registry;

my $reg = 'Bio::EnsEMBL::Registry';
my $host= 'ensembldb.ensembl.org';
my $user= 'anonymous';

$reg-&gt;load_registry_from_db(
  -host =&gt; $host,
  -user =&gt; $user
);

# Get adaptor to StructuralVariation object
my $sva = $reg-&gt;get_adaptor("human", "variation", "structuralvariation");

# Get the StructuralVariation object
my $sv = $sva-&gt;fetch_by_name('esv2663683');

print $sv-&gt;variation_name, " ",  $sv-&gt;var_class, " (SO term: ", $sv-&gt;class_SO_term, ")\n";

# Get Study information
if ($sv-&gt;study) {
  print "This structural variant comes from the study ", $sv-&gt;source_name, "-", $sv-&gt;study-&gt;name, ": ", $sv-&gt;study-&gt;description, "\n";
}
</pre>
<p>Like the variation, you can fetch the structural variations using a Slice.</p>
<pre class="code sh_perl">
#  Get adaptor to Slice object from core
my $sa = $reg-&gt;get_adaptor("human", "core", "slice");

my $slice = $sa-&gt;fetch_by_region('chromosome', 5, 1, 1000000);

# get all structural variation features on the slice
my $svfs = $slice-&gt;get_all_StructuralVariationFeatures();

# StructuralVariationFeature objects
foreach my $svf (@$svfs) {
  print $svf-&gt;variation_name, " ", $svf-&gt;seq_region_name, ":", $svf-&gt;seq_region_start, "-", $svf-&gt;seq_region_end, " ", 
	$svf-&gt;var_class, " (SO term: ", $svf-&gt;class_SO_term, ")\n";
}
</pre>
<p>
You can also retrieve all the supporting evidences associated to a structural variant.
</p>
<pre class="code sh_perl">
# Get adaptor to StructuralVariation object from the Ensembl Variation API
my $sva = $reg-&gt;get_adaptor("human", "variation", "structuralvariation");

# Get the StructuralVariation object
my $sv = $sva-&gt;fetch_by_name('esv2663683');

# Get the supporting evidences associated to the structural variation
my $ssvs = $sv-&gt;get_all_SupportingStructuralVariants();

foreach my $ssv (@$ssvs) {
  print $ssv-&gt;variation_name, " ",  $ssv-&gt;var_class, " (SO term: ", $ssv-&gt;class_SO_term, ")\n";
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1DBSQL_1_1SliceAdaptor.html">SliceAdaptor</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1StructuralVariationAdaptor.html">StructuralVariationAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Slice.html">Slice</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Study.html">Study</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1StructuralVariation.html">StructuralVariation</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1StructuralVariationFeature.html">StructuralVariationFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1SupportingStructuralVariation.html">SupportingStructuralVariation</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>


<br /><hr />
<h2 id="other" style="font-size:large">Others</h2>

<h2 id="iterators">Fetching Iterators</h2>

<p>
Sometimes an adaptor method may return more objects than can fit into memory at once, and for such
cases the Ensembl Variation API provides methods to fetch Iterator objects instead of the usual array reference.
An Iterator is an object which allows you to iterate over the entire set of objects fetched by the 
adaptor without loading them all into memory at once, by calling the next() method to fetch each object
in turn. You can tell when you have finished iterating over the set of objects
by using the has_next() method which returns true when the Iterator still has objects to fetch and false
when it is exhausted. These are the most 
commonly used Iterator methods, but there are also some other useful methods which allow you to transform
and filter the set of objects in an analogous way to using map and grep on standard perl arrays, using
the methods called (imaginatively) map() and grep(). Various other methods are also supported, please 
see the Iterator API documentation for full details.
</p>

<p>
Some example code using Iterators to fetch somatic mutations from the VariationAdaptor is listed below.
After the first fetch_Iterator_somatic() call we just iterate over all somatic mutations found in the
Ensembl Variation database, using next() to fetch each object, and has_next() to check when we are done. In the 
second call we filter the set of mutations down to only those which have
a source of 'COSMIC' using the grep() method. To do this we pass an anonymous subroutine as the only 
argument to grep() which checks each Variation object in turn (set to $_ for convenience in the 
subroutine) and returns a true value if the source is equal to 'COSMIC' and false otherwise. 
This anonymous subroutine is very similar to a block that
you would use when using perl's own grep function, but you have to prefix it with the 'sub' keyword.
In the third call to fetch_Iterator_somatic we use the map() method to return the name of each Variation
rather than the object itself using an anonymous subroutine just as you would use a block for perl's map
function. In the final call we combine the grep() and map() methods to return an Iterator over all the
names of somatic mutations from COSMIC. The map() and grep() methods return a new Iterator and only do
their transforming and filtering on each object in turn, without needing all objects in memory, they
are therefore much more memory-efficient than mapping and grepping a native perl array.
</p>

<pre class="code sh_perl">
use strict;
use warnings;

use Bio::EnsEMBL::Registry;

my $reg = 'Bio::EnsEMBL::Registry';

$reg-&gt;load_registry_from_db(
  -host =&gt; 'ensembldb.ensembl.org',
  -user =&gt; 'anonymous',
);

my $va = $reg-&gt;get_adaptor('human', 'variation', 'variation') or die;

my $somatic_iter = $va-&gt;fetch_Iterator_somatic();

while ($somatic_iter-&gt;has_next()) {
  my $mutation = $somatic_iter-&gt;next();
  print $mutation-&gt;name(), "\n";
}

my $cosmic_iter = $va-&gt;fetch_Iterator_somatic()-&gt;grep(sub {$_-&gt;source_name eq 'COSMIC'});

while ($cosmic_iter-&gt;has_next()) {
  print $cosmic_iter-&gt;next()-&gt;name(), "\n";
}

my $name_iter = $va-&gt;fetch_Iterator_somatic()-&gt;map(sub {$_-&gt;name});

while ($name_iter-&gt;has_next()) {
  print $name_iter-&gt;next(), "\n";
}

my $cosmic_name_iter = $va-&gt;fetch_Iterator_somatic()-&gt;grep(sub {$_-&gt;source_name eq 'COSMIC'})-&gt;map(sub {$_-&gt;name});

while ($cosmic_name_iter-&gt;has_next()) {
  print $cosmic_name_iter-&gt;next(), "\n";
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationAdaptor.html">VariationAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>


<br />
<h2 id="slice">Fetching variation features or structural variation features from a Slice</h2>
<p>Variation features and structural variation features can be fetched using a Slice object (from the <a href="/info/docs/api/core/core_tutorial.html#slices">Core API</a>).</p>
<pre class="code sh_perl">
# Slice adaptor
my $slice_adaptor = $registry-&gt;get_adaptor( 'Human', 'Core', 'Slice' );
# Slice object
my $slice = $slice_adaptor-&gt;fetch_by_region('chromosome', 3, 125439614, 125462946, 1);

# Fetch the variation feature objects overlapping the Slice
my @var_features = @{$slice-&gt;get_all_VariationFeatures()};

# Fetch the structural variation feature objects overlapping the Slice
my @sv_features = @{$slice-&gt;get_all_StructuralVariationFeatures()};

print "Number of variations: ", scalar @var_features, "\n";
print "Number of structural variations: ", scalar @sv_features, "\n";
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1DBSQL_1_1SliceAdaptor.html">SliceAdaptor</a> <i>(Core API)</i></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Slice.html">Slice</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1StructuralVariationFeature.html">StructuralVariationFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>

<p>It is also possible to fetch all the variation features overlapping a structural variation feature, using a Slice through a structural variation feature object.</p>

<pre class="code sh_perl">
# Structural variation adaptor
my $sv_adaptor = $registry-&gt;get_adaptor( 'human', 'variation', 'structuralvariation' );
# Structural variation object
my $sv = $sv_adaptor-&gt;fetch_by_name('esv2663683');

# Structural variation feature(s)
foreach my $svf (@{$sv-&gt;get_all_StructuralVariationFeatures()}){
  # Corresponding Slice
  my $slice = $svf-&gt;feature_Slice();

  # Fetch the variation feature objects overlapping the Slice
  my @var_features = @{$slice-&gt;get_all_VariationFeatures()};
  print "Number of variations: ", scalar (@var_features), "\n";
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1StructuralVariationAdaptor.html">StructuralVariationAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Slice.html">Slice</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1StructuralVariationFeature.html">StructuralVariationFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>

<p>And you can do the same with the variation features, for example.</p>
<pre class="code sh_perl">
# Variation adaptor
my $var_adaptor = $registry-&gt;get_adaptor( 'human', 'variation', 'variation' );
# Variation object
my $var = $var_adaptor-&gt;fetch_by_name('rs1333049');

# Variation feature(s)
foreach my $vf (@{$var-&gt;get_all_VariationFeatures()}){
  # Corresponding Slice
  my $slice = $vf-&gt;feature_Slice();

  # Fetch the structural variation feature objects overlapping the Slice
  my @svf_features = @{$slice-&gt;get_all_StructuralVariationFeatures()};
  print "Number of structural variations: ", scalar (@svf_features), "\n";
}
</pre>
<div class="code sh_perl" style="margin-bottom:15px">
  <div style="float:left;font-weight:bold">Used objects:</div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Adaptor objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1DBSQL_1_1VariationAdaptor.html">VariationAdaptor</a></li>
    </ul>
  </div>
  <div style="float:left;margin-left:10px;border-left:1px dotted #CCC">
    <span style="padding-left:0.9em">Main objects</span>
    <ul style="margin-bottom:0px">
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/core-api/classBio_1_1EnsEMBL_1_1Slice.html">Slice</a> <i>(Core API)</i></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1StructuralVariationFeature.html">StructuralVariationFeature</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1Variation.html">Variation</a></li>
      <li><a style="text-decoration:none" href="/info/docs/Doxygen/variation-api/classBio_1_1EnsEMBL_1_1Variation_1_1VariationFeature.html">VariationFeature</a></li>
    </ul>
  </div>
  <div style="clear:both"></div>
</div>

<br />
<h2 id="help">Further help</h2>

<p>
For additional information or help mail the <a href="http://lists.ensembl.org/mailman/listinfo/dev" rel="external">ensembl-dev</a> mailing list.
You will need to subscribe to this mailing list to use it.
More information on subscribing to any Ensembl mailing list is available from the 
<a href="http://www.ensembl.org/info/about/contact/">Ensembl Contacts</a> page.
</p>

</body>
</html>
